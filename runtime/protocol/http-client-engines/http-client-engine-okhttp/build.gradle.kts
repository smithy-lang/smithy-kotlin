/*
 * Copyright Amazon.com, Inc. or its affiliates. All Rights Reserved.
 * SPDX-License-Identifier: Apache-2.0
 */

description = "OkHttp Client Engine for Smithy services generated by smithy-kotlin"
extra["displayName"] = "Smithy :: Kotlin :: HTTP :: Engine :: OkHttp"
extra["moduleName"] = "aws.smithy.kotlin.runtime.http.engine.okhttp"

kotlin {
    jvm {
        compilations {
            val okHttp4Main by creating
            val okHttp5Main by creating

            val main by getting {
                tasks[compileKotlinTaskName].dependsOn(
                    okHttp4Main.compileKotlinTaskName,
                    okHttp5Main.compileKotlinTaskName,
                )
            }
        }
    }

    sourceSets {
        val commonMain by getting {
            dependencies {
                implementation(project(":runtime:runtime-core"))
                api(project(":runtime:protocol:http-client"))
                implementation(project(":runtime:observability:telemetry-defaults"))
            }
        }

        val jvmOkHttp4Main by getting {
            dependsOn(commonMain)
            dependencies {
                compileOnly(libs.okhttp4)
            }
        }

        val jvmOkHttp5Main by getting {
            dependsOn(commonMain)
            dependsOn(jvmOkHttp4Main) // API is forward-compatible so reuse as much as possible from the OkHttp4 adapter
            dependencies {
                compileOnly(libs.okhttp)
                compileOnly(libs.okhttp.coroutines)
            }
        }

        commonTest {
            dependencies {
                implementation(libs.kotlinx.coroutines.test)
            }
        }

        jvmMain {
            dependencies {
                dependsOn(jvmOkHttp4Main)
                dependsOn(jvmOkHttp5Main)

                runtimeOnly(libs.okhttp)
                runtimeOnly(libs.okhttp.coroutines)
            }
        }

        jvmTest {
            dependencies {
                implementation(project(":runtime:testing"))
                // use otel testing capabilities
                implementation(project(":runtime:observability:telemetry-provider-otel"))
                implementation(libs.opentelemetry.sdk.testing)
            }
        }

        all {
            languageSettings.optIn("aws.smithy.kotlin.runtime.InternalApi")
        }
    }
}
